apply plugin: 'com.android.application'
apply plugin: 'jacoco-everywhere'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'
apply plugin: 'realm-android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true" == System.getenv("TRAVIS")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")
def SUPPORT_LIB_VERSION = "25.3.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "org.fossasia.openevent"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 101
        versionName "1.0.1"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        fdroid {
        }
        googleplay {
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    // https://stackoverflow.com/questions/36833606/android-support-library-error-after-updating-to-23-3-0
    configurations.all {
        resolutionStrategy {
            force "com.android.support:support-annotation:${SUPPORT_LIB_VERSION}"
            force "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
            force "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
            force "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
            force "com.android.support:design:${SUPPORT_LIB_VERSION}"
            force "com.android.support:customtabs:${SUPPORT_LIB_VERSION}"
        }
    }

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:customtabs:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:palette-v7:${SUPPORT_LIB_VERSION}"
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.github.jasminb:jsonapi-converter:0.7'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.facebook.stetho:stetho:1.4.2'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    compile 'com.uphyca:stetho_realm:2.0.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

    // Googleplay Variant
    googleplayCompile 'com.google.android.gms:play-services-maps:9.8.0'

    // Fdroid Variant
    fdroidCompile 'org.osmdroid:osmdroid-android:5.1'
    fdroidCompile 'org.apache.directory.studio:org.slf4j.api:1.7.2'
    fdroidCompile 'org.slf4j:slf4j-simple:1.7.14'

    // Unit Test
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"

    //Instrumentation Test
    androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "org.mockito:mockito-core:1.10.19"

    // Catch all the leaks
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    //Branch integration for deep linking
    compile ('io.branch.sdk.android:library:2.5.9') {
        exclude module: 'answers-shim'
    }
}
