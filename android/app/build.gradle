apply plugin: 'com.android.application'
apply plugin: 'jacoco-everywhere'
apply plugin: 'android-apt'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def SUPPORT_LIB_VERSION = "24.2.1"
def BUTTER_KNIFE_VERSION = "8.4.0"
def RETROFIT_VERSION = "2.1.0"
def LEAK_CANARY_VERSION = "1.3.1"
def ESPRESSO_VERSION = "2.2.2"
def SUPPORT_TEST_LIB_VERSION = "0.5"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "org.fossasia.openevent"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 83
        versionName "0.8.3"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        fdroid {
        }
        googleplay {
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    // https://stackoverflow.com/questions/36833606/android-support-library-error-after-updating-to-23-3-0
    configurations.all {
        resolutionStrategy {
            force "com.android.support:support-annotation:${SUPPORT_LIB_VERSION}"
            force "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
            force "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
            force "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
            force "com.android.support:design:${SUPPORT_LIB_VERSION}"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile "com.jakewharton:butterknife:${BUTTER_KNIFE_VERSION}"
    apt "com.jakewharton:butterknife-compiler:${BUTTER_KNIFE_VERSION}"

    // Googleplay Variant
    googleplayCompile 'com.google.android.gms:play-services-maps:9.6.1'

    // Fdroid Variant
    fdroidCompile 'org.osmdroid:osmdroid-android:5.1'
    fdroidCompile 'org.apache.directory.studio:org.slf4j.api:1.7.2'
    fdroidCompile 'org.slf4j:slf4j-simple:1.7.14'

    // Unit Test
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"

    //Instrumentation Test
    androidTestCompile "com.android.support:support-annotations:${SUPPORT_LIB_VERSION}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"
    androidTestCompile "com.android.support.test:runner:${SUPPORT_TEST_LIB_VERSION}"
    androidTestCompile "com.android.support.test:rules:${SUPPORT_TEST_LIB_VERSION}"
    androidTestCompile "org.mockito:mockito-core:1.10.19"

    // Catch all the leaks
    debugCompile "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}" // or 1.4-beta1
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}" // or 1.4-beta1
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}" // or 1.4-beta1

    //fast scroll
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.9'
}
